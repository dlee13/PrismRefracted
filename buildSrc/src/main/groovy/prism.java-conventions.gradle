plugins {
    id 'java'
    id 'checkstyle'
    id 'io.franzbecker.gradle-lombok'
    id 'com.github.ben-manes.versions'
}

group = project.property('group')
description = project.property('description')

targetCompatibility = sourceCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()

    // Aikar
    maven { url = 'https://repo.aikar.co/content/groups/aikar/' }

    // CodeMC (nbt-api)
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }

    // Sonatype (kyori)
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }

    // Spigot
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }

    // TeamTriumph
    maven { url = 'https://repo.triumphteam.dev/snapshots' }
}

lombok {
    version = '1.18.24'
    sha256 = ''
}

def getBuildNumber = {
    // get the name of the last tag
    def tagInfo = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = tagInfo
    }
    tagInfo = tagInfo.toString()

    if (!tagInfo.contains('-')) {
        return 0
    }
    return tagInfo.split("-")[1]
}

project.ext.fullVersion = version = project.property('version') + '-b' + getBuildNumber()
