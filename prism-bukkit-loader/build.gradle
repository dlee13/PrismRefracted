plugins {
    id 'prism.java-conventions'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'xyz.jpenilla.run-paper' version '1.0.6'
    id 'de.eldoria.plugin-yml.bukkit' version '0.7.1'
}

dependencies {
    compileOnly project.property('deps.adventure-api')
    compileOnly project.property('deps.adventure-bukkit')
    compileOnly project.property('deps.adventure-minimessage')
    compileOnly project.property('deps.adventure-plain')
    compileOnly project.property('deps.log4j-api')
    compileOnly project.property('deps.spigot-api')
    implementation project(':prism-loader')
}

bukkit {
    name = 'prism'
    main = 'network.darkhelmet.prism.PrismBukkitPluginLoader'
    apiVersion = '1.17'
    authors = ['viveleroi']
    website = project.property('website')
    load = 'STARTUP'
}

shadowJar {
    archiveFileName = "prism-bukkit-${project.ext.fullVersion}.jar"

    from {
        project(':prism-bukkit').tasks.shadowJar.archiveFile
    }

    relocate 'com.fasterxml.jackson', 'network.darkhelmet.prism.libs.jackson'
    relocate 'org.objectweb.asm', 'network.darkhelmet.prism.asm'
    relocate 'org.spongepowered.configurate', 'network.darkhelmet.prism.libs.configurate'
    relocate 'com.typesafe', 'network.darkhelmet.prism.libs.typesafe'
    relocate 'io.leangen.geantyref', 'network.darkhelmet.prism.libs.geantyref'
    relocate 'net.kyori.adventure', 'network.darkhelmet.prism.libs.adventure'
    relocate 'net.kyori.examination', 'network.darkhelmet.prism.libs.examination'
}

artifacts {
    archives shadowJar
}

runServer {
    // Configure the Minecraft version for our task.
    // This is the only required configuration besides applying the plugin.
    // Your plugin's jar (or shadowJar if present) will be used automatically.
    minecraftVersion('1.19.4')
}